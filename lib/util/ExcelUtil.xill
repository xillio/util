/**
 * Project: Util Library
 * Author: Titus Nachbauer
 * Date: 2016-11-21
 *
 * ExcelUtil.xill is a library of reusable functions related to Excel and mapping.
 * It is recommended to include it in your project's Transformation robot.
 */
use Excel, Collection, Assert, MySQL, System, String;

//Returns an OBJECT containing a mapping for every sheet in the Excel workbook stored at path
function getMappingsFromExcel(path) {
    var workbook = Excel.loadWorkbook(path);
    var mappings = {};
    
    foreach(sheetName in Excel.getSheetNames(workbook)) {
        mappings[sheetName] = getMappingFromSheet(workbook, sheetName);
    }
    return mappings;
}

//Returns a mapped value using a mapping OBJECT generated by getMappingsFromExcel()
function getMappedValue(sourceValue, mappings, mappingName, defaultValue) {
    var result = null;
    if (mappings[mappingName] != null) {
        result = mappings[mappingName][sourceValue];
    }
    if (result == null) {
        result = defaultValue;
    }
    return result;
}

//Returns an OBJECT containing a source target mapping, assuming the A column of sheetName contains source values
private function getMappingFromSheet(workbook, sheetName) {
    var result = {};
    var sheet = Excel.loadSheet(workbook, sheetName);
    var source = collect(Excel.getColumn(sheet, "A"));
    var target = collect(Excel.getColumn(sheet, "B"));
    
    Assert.isTrue (Collection.length(source) == Collection.length(target));
    
    foreach (index, element in source) {
        if (index == 0) {
            continue; //skip first line (containing headers)
        }
        result[element] = target[index];
    }
    return result;
}

// inserts headers into sheet on the first row
// Element 0 of headers goes in cell A1, element 1 in cell A2 etcetera
function insertHeaders(sheet,headers) {
	var i=1;
	foreach(header in headers) {
		header = String.replace(header,"^_formula_","");
		Excel.setCellValue(sheet, i, 1, header);
		i++;
	}
}

// inserts rowData object into row rowNumber of sheet.
// mimics MySQL.storeObject() functionality.
// headers should be the same variable as used in insertHeaders,
// rowData must have the headers as keys
function insertRow(sheet,rowData,rowNumber,headers) {
	var rowDataTemp = Collection.duplicate(rowData);
	var columnNumber=1;
	foreach(header in headers) {
		if(String.startsWith(header, "_formula_")) {
			do {
			Excel.setCellFormula(sheet, columnNumber, rowNumber, rowDataTemp[header]);
			} fail {
				Excel.setCellValue(sheet, columnNumber, rowNumber, rowDataTemp[header]);
			}
		} else {
			Excel.setCellValue(sheet, columnNumber, rowNumber, rowDataTemp[header]);
		}
		Collection.remove(rowDataTemp, header);
		columnNumber++;
	}
	if(Collection.length(rowDataTemp)>0) {
		System.print("The row data does not comply with the headers specification. rowData: \r\n"
			:: rowDataTemp :: "\r\nHeaders: " :: headers, "warning");
	}
}

// uses insertRow() for each row in rowsData,
// inserts subsequently from firstRow to next rows
function insertListOfRows(sheet,rowsData,firstRow,headers) {
	var currentRow = firstRow;
	foreach(rowData in rowsData) {
		insertRow(sheet,rowData,currentRow,headers);
		currentRow++;
	}
}
